apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'
apply from: '../deps.gradle'
apply from: '../signing.gradle'


android {

    signingConfigs {
        config {
            keyAlias myKeyAlias
            keyPassword myKeyPassword
            storeFile myStoreFile
            storePassword myStorePassword
        }
    }

    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {

        applicationId "com.whomi"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 6
        versionName "0.1.0"
        multiDexEnabled true
        testInstrumentationRunner "com.whomi.MockTestRunner"

    }

    lintOptions {
        abortOnError false
    }

    buildTypes {

        release {
            def gitSha = "git rev-parse --short HEAD".execute([], project.rootDir).text.trim()
            def buildTime = new GregorianCalendar().format("dd-MM-yyyy 'HH:mm:ss'")
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            buildConfigField "boolean", "REPORT_CRASHES", "true"
            buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
            buildConfigField "String", "BACKEND_URL", $/"https://whomi.herokuapp.com/"/$
            buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        }

        debug {
            buildConfigField "boolean", "REPORT_CRASHES", "true"
            buildConfigField "String", "BACKEND_URL", $/"https://whomi.herokuapp.com/"/$
            buildConfigField "String", "GIT_SHA", "\"DEBUG\""
            buildConfigField "String", "BUILD_TIME", "\"DEBUG\""
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            minifyEnabled false
        }
    }


    productFlavors {

        dev {

            minSdkVersion 21
        }

        dev_normal {

        }

        production {

        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.code.gson/gson/pom.properties'
        exclude 'META-INF/maven/com.google.code.gson/gson/pom.xml'
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

}

dependencies {

    ///

    // test deps
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.1'
    testCompile 'com.github.fabioCollini:DaggerMock:0.6.4'
    testApt 'com.google.dagger:dagger-compiler:2.5'

    // android test deps
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:2.7.14'
    androidTestCompile 'org.mockito:mockito-android:2.7.14'
    androidTestCompile 'com.android.support:support-annotations:25.2.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile "com.google.dexmaker:dexmaker:1.2"
    androidTestApt 'com.google.dagger:dagger-compiler:2.5'

    //support
    compile 'com.android.support:support-annotations:25.2.0'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:support-v4:25.2.0'

    //gson
    compile 'com.google.code.gson:gson:2.8.0'

    //okhttp+retrofit
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'

    //rx
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.3'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'nl.littlerobots.rxlint:rxlint:1.0'

    //butterknife
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'

    //timber
    compile 'com.jakewharton.timber:timber:3.0.2'

    //autovalue
    compile 'com.google.auto.value:auto-value:1.2'
    apt 'com.ryanharter.auto.value:auto-value-gson:0.4.6'
    provided 'com.ryanharter.auto.value:auto-value-gson:0.4.6'
    apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    //dagger
    apt 'com.google.dagger:dagger-compiler:2.5'
    compile 'com.google.dagger:dagger:2.5'
    provided 'javax.annotation:jsr250-api:1.0'

    //leak canary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    compile 'com.google.guava:guava:19.0'
    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'com.squareup:tape:1.2.3'

    compile 'com.android.support:multidex:1.0.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true
    }
    compile 'com.google.android.gms:play-services-analytics:10.2.0'

    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile 'uk.co.chrisjenx:calligraphy:2.2.0'

}

retrolambda {
    jdk jdk_path
    oldJdk oldJdk_path
    javaVersion JavaVersion.VERSION_1_7
}

apply plugin: 'com.google.gms.google-services'
